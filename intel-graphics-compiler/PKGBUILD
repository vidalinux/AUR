# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Torsten Ke√üler <tpkessler@archlinux.org>

pkgname=intel-graphics-compiler
pkgver=1.0.16510.1
_llvmmaj=15
_llvmver="${_llvmmaj}.0.7"
pkgrel=1
epoch=1
pkgdesc='Intel Graphics Compiler for OpenCL'
arch=('x86_64')
url='https://github.com/intel/intel-graphics-compiler/'
license=('MIT' 'custom')
depends=('gcc-libs' 'zlib' 'spirv-tools=2024.1' 'llvm15' 'llvm15-libs' 'lld15' 
         'spirv-llvm15-translator=15.0.0' 'intel-opencl-clang=15.0.0' 'vc-intrinsics=0.18.0-2' )

makedepends=('git' 'cmake' 'python' 'python-mako' 'spirv-headers=1:1.3.280.0')
options=('!emptydirs' '!lto')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/intel/intel-graphics-compiler/archive/refs/tags/igc-${pkgver}.tar.gz")
sha256sums=('SKIP')

prepare() {
    sed -e '/Werror/d' -i intel-graphics-compiler-igc-${pkgver}/IGC/CMakeLists.txt || die
    sed -e '/-D_FORTIFY_SOURCE=2/d' -i intel-graphics-compiler-igc-${pkgver}/IGC/CMakeLists.txt || die
}

build() {
    LDFLAGS+=" -Wl,-z,undefs"

    EMAIL='builduser@archlinux.org' \
    cmake -B build -S "${pkgname}-igc-${pkgver}" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DBUILD_SHARED_LIBS:BOOL=ON \
        -DIGC_OPTION__ARCHITECTURE_TARGET=Linux64 \
        -DIGC_OPTION__SPIRV_TOOLS_MODE=Prebuilds \
        -DIGC_OPTION__USE_PREINSTALLED_SPIRV_HEADERS=ON \
        -DIGC_OPTION__LLD_MODE=Prebuilds \
        -DIGC_OPTION__LLDELF_H_DIR=/usr/lib/llvm${_llvmmaj}/include/lld/Common \
        -DIGC_OPTION__CLANG_MODE=Prebuilds \
        -DCCLANG_INSTALL_PREBUILDS_DIR=/usr/lib \
        -DCCLANG_FROM_SYSTEM=ON \
        -DIGC_OPTION__OPENCL_HEADER_PATH=/usr/include/cclang/opencl-c.h \
        -DIGC_OPTION__LLVM_PREFERRED_VERSION="${_llvmver}" \
        -DIGC_OPTION__LLVM_MODE=Prebuilds \
        -DLLVM_ROOT=/usr/lib/llvm${_llvmmaj}/ \
        -DIGC_OPTION__SPIRV_TRANSLATOR_MODE=Prebuilds \
        -DIGC_OPTION__USE_PREINSTALLED_SPIRV_HEADERS=ON \
        -DIGC_OPTION__LINK_KHRONOS_SPIRV_TRANSLATOR=ON \
        -DIGC_OPTION__VC_INTRINSICS_MODE=Prebuilds \
        -DVCIntrinsics15_DIR=/usr/lib/llvm15/lib/cmake/VCIntrinsics15 \
        -DIGC_BUILD__VC_ENABLED=ON \
        -DINSTALL_GENX_IR=ON \
        -DSPIRVLLVMTranslator_INCLUDE_DIR=/usr/include/LLVMSPIRVLib/ \
        -G Ninja
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
    install -D -m644 "${pkgname}-igc-${pkgver}"/LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
    mv "${pkgdir}/usr/lib/igc/NOTICES.txt" "${pkgdir}/usr/share/licenses/${pkgname}"
}
